load("@kong_bindings//:variables.bzl", "KONG_VAR")
load("//build:build_system.bzl", "kong_directory_genrule", "kong_rules_group", "kong_template_file")

exports_files([
    "package/nfpm.yaml",
])

lib_deps = [
    "@openssl",  #TODO: select over fips (but select doesn't work in list comprehension)
]

install_lib_deps_cmd = "\n".join([
    """
    DEP=${WORKSPACE_PATH}/$(echo $(locations %s) | awk '{print $1}')
    # use tar magic to exclude files and create with correct permission
    copy_with_filter ${DEP} ${BUILD_DESTDIR}/kong
""" % dep
    for dep in lib_deps
])

lualib_deps = [
    "@lua-kong-nginx-module//:all_srcs",
    "@lua-resty-lmdb//:all_srcs",
    "@lua-resty-events//:all_srcs",
    "@lua-resty-websocket//:all_srcs",
    "@atc_router",
]

install_lualib_deps_cmd = "\n".join([
    """
    DEP=$(pwd)/external/%s
    # TODO: fix atc_router makefile so that we can choose to only install lualib
    sed -i -e '/libatc_router.so/d' ${DEP}/Makefile
    INSTALL=/usr/bin/install make --silent -C ${DEP} LUA_LIB_DIR=${BUILD_DESTDIR}/openresty/lualib install
""" % dep.lstrip("@").split("/")[0]
    for dep in lualib_deps
])

wasmx_vm_deps = select({
    "@kong//:wasmx_v8": [
        "@v8//:lib",
    ],
    "@kong//:wasmx_wasmer": [
        "@wasmer//:lib",
    ],
    "@kong//:wasmx_wasmtime": [
        "@wasmtime//:lib",
    ],
    "//conditions:default": [],
})

wasmx_deps = select({
    "@kong//:wasmx_static_mod_flag": [
        "@ngx_wasm_module//:lua_libs",
    ],
    "@kong//:wasmx_dynamic_mod_flag": [
        "@ngx_wasm_module//:lua_libs",
    ],
    "//conditions:default": [],
}) + wasmx_vm_deps

wasm_libs_install = """
    for fname in $(locations @ngx_wasm_module//:lua_libs); do
        base=${fname##*/ngx_wasm_module/lib/}
        dest="${BUILD_DESTDIR}/openresty/lualib/$base"
        mkdir -p "$(dirname "$dest")"
        cp -v "$fname" "$dest"
    done
"""

wasm_libs_cmd = select({
    "@kong//:wasmx_static_mod_flag": wasm_libs_install,
    "@kong//:wasmx_dynamic_mod_flag": wasm_libs_install,
    "//conditions:default": "\n",
})

wasmx_vm_cmd = select({
    "@kong//:wasmx_v8": """
    if [[ -d ${BUILD_DESTDIR}/openresty/nginx/lib ]]; then
        copy_with_filter ${BUILD_DESTDIR}/openresty/nginx/lib ${BUILD_DESTDIR}/kong/lib
        rm -rf ${BUILD_DESTDIR}/openresty/nginx/lib
    fi
""",
    "@kong//:wasmx_wasmer": """
    if [[ -d ${BUILD_DESTDIR}/openresty/nginx/lib ]]; then
        copy_with_filter ${BUILD_DESTDIR}/openresty/nginx/lib ${BUILD_DESTDIR}/kong/lib
        rm -rf ${BUILD_DESTDIR}/openresty/nginx/lib
    fi
""",
    # both v8 and wasmer currently depend on openresty/nginx/lib/libngx_wasm_rs.so,
    # but in the case of wasmtime it is statically linked and thus not needed in
    # the final package
    "@kong//:wasmx_wasmtime": """
    if [[ -d ${BUILD_DESTDIR}/openresty/nginx/lib ]]; then
        rm -rf ${BUILD_DESTDIR}/openresty/nginx/lib
    fi
""",
    "//conditions:default": "",
})

wasmx_load_module = select({
    "@kong//:wasmx_dynamic_mod_flag": """
  if ! grep -q "ngx_wasm_module.so" kong/templates/nginx.lua; then
      WASM_LINE_TPL=$((`grep -n -m1 "error_log"  kong/templates/nginx.lua | cut -d: -f1` + 1))
      WASM_LINE_FXT=$((`grep -n -m1 "error_log"  spec/fixtures/custom_nginx.template | cut -d: -f1` + 1))
      if [[ "$OSTYPE" == "darwin"* ]]; then
          gsed -i "$WASM_LINE_TPL a load_module ${BUILD_DESTDIR}/openresty/nginx/modules/ngx_wasm_module.so;" kong/templates/nginx.lua
          gsed -i "$WASM_LINE_FXT a load_module ${BUILD_DESTDIR}/openresty/nginx/modules/ngx_wasm_module.so;" spec/fixtures/custom_nginx.template
      else
          sed -i "$WASM_LINE_TPL a load_module ${BUILD_DESTDIR}/openresty/nginx/modules/ngx_wasm_module.so;" kong/templates/nginx.lua
          sed -i "$WASM_LINE_FXT a load_module ${BUILD_DESTDIR}/openresty/nginx/modules/ngx_wasm_module.so;" spec/fixtures/custom_nginx.template
      fi
  fi
""",
    "//conditions:default": "",
})

kong_directory_genrule(
    name = "kong",
    srcs = [
        "@luarocks//:luarocks_make",
        "@luarocks//:luarocks_target",
        "@openresty",
        "@openresty//:luajit",
        "@protoc//:all_srcs",
    ] + lib_deps + lualib_deps + wasmx_deps,
    cmd =
        """ set -e
        function copy_with_filter {
            mkdir -p $2
            tar -cC $1 --exclude="*.a" --exclude="*.la" \
                        --exclude="*/share/*" --exclude="*/bin/*" \
                        --exclude="*.log" . | tar -xC $2/.
            chmod -R "+rw" $2
        }
        function LN {
            if [[ "$OSTYPE" == "darwin"* ]]; then
                # TODO: support relative path links once we start to cross compile on macOS
                ln -sf $@
            else
                ln -srf $@
            fi
        }
        rm -rf ${BUILD_DESTDIR}
        mkdir -p ${BUILD_DESTDIR}/kong/lib ${BUILD_DESTDIR}/openresty ${BUILD_DESTDIR}/bin

        if [[ "$OSTYPE" == "darwin"* ]]; then
            libext="dylib"
        else # assume linux
            libext="so"
        fi

        OPENRESTY=${WORKSPACE_PATH}/$(echo '$(locations @openresty//:openresty)' | awk '{print $1}')
        cp -r ${OPENRESTY}/. ${BUILD_DESTDIR}/openresty/.
        LN ${BUILD_DESTDIR}/openresty/bin/resty ${BUILD_DESTDIR}/bin/resty
        chmod -R "+rw" ${BUILD_DESTDIR}/openresty

        LUAJIT=${WORKSPACE_PATH}/$(echo '$(locations @openresty//:luajit)' | awk '{print $1}')
        copy_with_filter ${LUAJIT} ${BUILD_DESTDIR}/openresty/luajit
        cp ${LUAJIT}/bin/luajit ${BUILD_DESTDIR}/openresty/luajit/bin/luajit
        tar -cC ${LUAJIT}/share . | tar -xC ${BUILD_DESTDIR}/openresty/luajit/share
        chmod -R "+rw" ${BUILD_DESTDIR}/openresty/luajit

        LUAROCKS=${WORKSPACE_PATH}/$(dirname '$(location @luarocks//:luarocks_make)')/luarocks_tree
        cp -r ${LUAROCKS}/. ${BUILD_DESTDIR}/.
        rm ${BUILD_DESTDIR}/bin/lapis ${BUILD_DESTDIR}/bin/luarocks-admin

        ATC_ROUTER=${WORKSPACE_PATH}/$(location @atc_router)
        cp $ATC_ROUTER ${BUILD_DESTDIR}/openresty/lualib/.
    """ + wasmx_load_module +
        """
        cp -r $(locations @protoc//:all_srcs) ${BUILD_DESTDIR}/kong/.

    """ + install_lib_deps_cmd + install_lualib_deps_cmd + wasm_libs_cmd + wasmx_vm_cmd +
        """
        mkdir -p ${BUILD_DESTDIR}/etc/kong
        cp kong.conf.default ${BUILD_DESTDIR}/etc/kong/kong.conf.default

        # housecleaning
        mv ${BUILD_DESTDIR}/kong/*.${libext}* ${BUILD_DESTDIR}/kong/lib 2>/dev/null || true
        if [[ -d ${BUILD_DESTDIR}/kong/lib64 ]]; then
            copy_with_filter ${BUILD_DESTDIR}/kong/lib64 ${BUILD_DESTDIR}/kong/lib
            rm -rf ${BUILD_DESTDIR}/kong/lib64
        fi

        # remove pkgconfig since they are invalid anyway
        find ${BUILD_DESTDIR} -name "*.pc" -delete

        # clean empty directory
        find ${BUILD_DESTDIR} -empty -type d -delete

        # foreign_cc rule dereferences symlink, we will dedup them here
        # TODO: patch https://github.com/bazelbuild/rules_foreign_cc/blob/main/foreign_cc/private/framework.bzl#L450 to not remove symlink
        for f in $(find ${BUILD_DESTDIR}/kong/lib ${BUILD_DESTDIR}/openresty/luajit/lib -type f -name "*.${libext}*" ); do
            if [[ -L "$f" ]]; then continue; fi # already a symlink
            target=$(ls -r1 $f.* 2>/dev/null | head -n1)
            if [[ ! -z "$target" && "$f" != "$target" ]]; then
                LN "$target" "$f"
            fi
        done
        LN ${BUILD_DESTDIR}/openresty/nginx/sbin/nginx ${BUILD_DESTDIR}/openresty/bin/openresty
    """,
    # XXX: bazel forces 0555 as artifact permission, which is not correct for packagin
    # here we deliberately use a different directory so file permission is preserved
    # see also https://github.com/bazelbuild/bazel/issues/5588
    output_dir = KONG_VAR["BUILD_NAME"] + ".nop",
    visibility = ["//visibility:public"],
)

kong_template_file(
    name = "venv.sh",
    output = "%s-venv.sh" % KONG_VAR["BUILD_NAME"],
    substitutions = {
        "{{build_name}}": KONG_VAR["BUILD_NAME"],
        "{{workspace_path}}": KONG_VAR["WORKSPACE_PATH"],
    },
    template = "//build:templates/venv.sh",
)

kong_template_file(
    name = "venv.fish",
    output = "%s-venv.fish" % KONG_VAR["BUILD_NAME"],
    substitutions = {
        "{{build_name}}": KONG_VAR["BUILD_NAME"],
        "{{workspace_path}}": KONG_VAR["WORKSPACE_PATH"],
    },
    template = "//build:templates/venv.fish",
)

kong_template_file(
    name = "venv-commons",
    is_executable = True,
    output = "%s-venv/lib/venv-commons" % KONG_VAR["BUILD_NAME"],
    substitutions = {
        "{{workspace_path}}": KONG_VAR["WORKSPACE_PATH"],
    },
    template = "//build:templates/venv-commons",
)

kong_rules_group(
    name = "venv",
    propagates = [
        ":kong",
        ":venv.sh",
        ":venv.fish",
        ":venv-commons",
    ],
    visibility = ["//visibility:public"],
)
