load("@rules_foreign_cc//foreign_cc:defs.bzl", "make")
load("@kong_bindings//:variables.bzl", "KONG_VAR")

filegroup(
    name = "all_srcs",
    srcs = glob(
        include = ["**"],
        exclude = ["*.bazel"],
    ),
)

make(
    name = "passwdqc",
    lib_source = ":all_srcs",
    out_include_dir = None,  # don't install headers
    out_lib_dir = "",
    out_shared_libs = select({
        "@platforms//os:macos": [
            "libpasswdqc.dylib",
        ],
        "//conditions:default": [
            "libpasswdqc.so.1",
        ],
    }),
    # -a preserve links
    postfix_script = select({
        "@platforms//os:macos": "cp -a libpasswdqc.1.dylib libpasswdqc.dylib $$INSTALLDIR$$/",
        "//conditions:default": "cp -a libpasswdqc.so.1 libpasswdqc.so $$INSTALLDIR$$/",
    }),
    targets =
        select({
            "@platforms//os:macos": [
                "libpasswdqc.dylib -j" + KONG_VAR["NPROC"] + " SHARED_LIB=libpasswdqc.1.dylib DEVEL_LIB=libpasswdqc.dylib",
            ],
            "//conditions:default": [
                "libpasswdqc.so -j" + KONG_VAR["NPROC"],
            ],
        }),
    visibility = ["//visibility:public"],
)
