load("@rules_foreign_cc//foreign_cc:defs.bzl", "configure_make", "make")
load("@kong_bindings//:variables.bzl", "KONG_VAR")
load("@openresty_binding//:variables.bzl", "LUAJIT_VERSION")

filegroup(
    name = "luajit_srcs",
    srcs = glob(
        include = ["bundle/LuaJIT*/**"],
    ),
)

genrule(
    name = "luajit_xcflags",
    outs = ["luajit_xcflags.txt"],
    cmd = "macos=" + select({
              "@platforms//os:macos": "1",
              "//conditions:default": "0",
          }) + "\n" +
          "aarch64=" + select({
              "@platforms//cpu:arm64": "1",
              "//conditions:default": "0",
          }) + "\n" +
          "debug=" + select({
              "@kong//:debug_flag": "1",
              "//conditions:default": "0",
          }) + "\n" +
          "cross=" + select({
              "@kong//:any-cross": "1",
              "//conditions:default": "0",
          }) +
          """
    flags="-DLUAJIT_ENABLE_LUA52COMPAT -DLUAJIT_VERSION=\\\\\\"{luajit_version}\\\\\\""
    if [[ $$debug -eq 1 ]]; then
        flags="$$flags -DLUA_USE_ASSERT -DLUA_USE_APICHECK"
        if [[ $$macos -ne 1 ]]; then
            if [[ $$cross -ne 1 ]]; then
                flags="$$flags -DLUA_USE_VALGRIND"
            fi
            if [[ $$aarch64 -ne 1 ]]; then
                flags="$$flags -DLUAJIT_USE_SYSMALLOC"
            fi
        fi
    fi

    if [[ $$macos -eq 1 ]]; then
        flags="$$flags -fno-stack-check"
    fi

    echo "$$flags" >$@

    """.format(luajit_version = LUAJIT_VERSION),
)

make(
    name = "luajit",
    args = [
        "LDFLAGS=\"-Wl,-rpath,%s/kong/lib\"" % KONG_VAR["INSTALL_DESTDIR"],  # make ffi.load happy, even when it's invoked without nginx
        "XCFLAGS=\"$(cat $$EXT_BUILD_ROOT$$/$(execpath :luajit_xcflags))\"",
        "LUA_ROOT=%s/openresty/luajit" % KONG_VAR["INSTALL_DESTDIR"].rstrip("/"),
        "MACOSX_DEPLOYMENT_TARGET=" + KONG_VAR["MACOSX_DEPLOYMENT_TARGET"],
    ] + select({
        "@kong//:any-cross": [
            "HOST_CC=cc",
        ],
        "//conditions:default": [
        ],
    }),
    build_data = [
        ":luajit_xcflags",
    ],
    lib_source = ":luajit_srcs",
    out_binaries = [
        "luajit",
    ],
    out_shared_libs = select({
        "@platforms//os:macos": [
            "libluajit-5.1.2.dylib",
        ],
        "//conditions:default": [
            "libluajit-5.1.so.2",
        ],
    }),
    targets = [
        "-j" + KONG_VAR["NPROC"],
        "install",
    ],
    visibility = ["//visibility:public"],
)

CONFIGURE_OPTIONS = [
    "--with-pcre-jit",
    "--with-http_ssl_module",
    "--with-http_sub_module",
    "--with-http_realip_module",
    "--with-http_stub_status_module",
    "--with-http_v2_module",
    "--with-stream_realip_module",  # >= 1.11.4
    "--with-stream_ssl_preread_module",  # >= 1.11.5
    "--without-http_encrypted_session_module",
    "--with-luajit=$$EXT_BUILD_DEPS$$/luajit",
    "--with-cc-opt=\"-I$$EXT_BUILD_DEPS$$/pcre/include\"",
    "--with-cc-opt=\"-I$$EXT_BUILD_DEPS$$/openssl/include\"",
    "--with-cc-opt=\"-I$$EXT_BUILD_DEPS$$/luajit/include\"",
    "--with-ld-opt=\"-L$$EXT_BUILD_DEPS$$/pcre/lib\"",
    "--with-ld-opt=\"-L$$EXT_BUILD_DEPS$$/openssl/lib\"",
    "--with-ld-opt=\"-L$$EXT_BUILD_DEPS$$/luajit/lib\"",
    # here let's try not having --disable-new-dtags; --disable-new-dtags creates runpath instead of rpath
    # note runpath can't handle indirect dependency (nginx -> luajit -> dlopen("other")), so each indirect
    # dependency should have its rpath set (luajit, libxslt etc); on the other side, rpath is not
    # overridable by LD_LIBRARY_PATH and it may cause trouble debugging, so we _should_ prefer runpath.
    # if it doesn't work, then add --disable-new-dtags back
    "--with-ld-opt=\"-Wl,-rpath,%s/kong/lib\"" % KONG_VAR["INSTALL_DESTDIR"],
    "-j%s" % KONG_VAR["NPROC"],

    # options from our customed patch
    "--with-install-prefix=%s" % KONG_VAR["INSTALL_DESTDIR"],

    # Note $$EXT_BUILD_ROOT$$ is bazel variable not from environment variable
    # which points to the directory of current WORKSPACE

    # external modules
    "--add-module=$$EXT_BUILD_ROOT$$/external/lua-kong-nginx-module",
    "--add-module=$$EXT_BUILD_ROOT$$/external/lua-kong-nginx-module/stream",
    "--add-module=$$EXT_BUILD_ROOT$$/external/lua-resty-lmdb",
    "--add-module=$$EXT_BUILD_ROOT$$/external/lua-resty-events",
] + select({
    "@kong//:arm64-linux-gnu-cross": [
        "--crossbuild=Linux:aarch64",
        "--with-endian=le",
        "--with-int=4",
        "--with-long=8",
        "--with-long-long=8",
        "--with-ptr-size=8",
        "--with-sig-atomic-t=4",
        "--with-size-t=8",
        "--with-off-t=8",
        "--with-time-t=8",
        "--with-sys-nerr=132",
    ],
    "@kong//:x86_64-linux-musl-cross": [
        "--crossbuild=Linux:x86_64",
        "--with-endian=le",
        "--with-int=4",
        "--with-long=8",
        "--with-long-long=8",
        "--with-ptr-size=8",
        "--with-sig-atomic-t=4",
        "--with-size-t=8",
        "--with-off-t=8",
        "--with-time-t=8",
        "--with-sys-nerr=132",
    ],
    "//conditions:default": [],
}) + select({
    "@kong//:any-cross": [
        "--with-cc-opt=\"-I$$EXT_BUILD_DEPS$$/zlib/include\"",
        "--with-ld-opt=\"-L$$EXT_BUILD_DEPS$$/zlib/lib\"",
    ],
    "//conditions:default": [],
}) + select({
    # any cross build that migrated to use libxcrypt needs those flags
    # alpine uses different libc so doesn't need it
    "@kong//:arm64-linux-gnu-cross": [
        "--with-cc-opt=\"-I$$EXT_BUILD_DEPS$$/libxcrypt/include\"",
        "--with-ld-opt=\"-L$$EXT_BUILD_DEPS$$/libxcrypt/lib\"",
    ],
    "//conditions:default": [],
}) + select({
    "@kong//:debug_flag": [
        "--with-debug",
        "--with-no-pool-patch",
        "--with-cc-opt=\"-DNGX_LUA_USE_ASSERT -DNGX_LUA_ABORT_AT_PANIC\"",
    ],
    "//conditions:default": [],
}) + select({
    "@kong//:fips_flag": [
        "--with-cc-opt=\"-I$$EXT_BUILD_DEPS$$/include\"",
        "--with-ld-opt=\"-L$$EXT_BUILD_DEPS$$/lib -Wl,-Bsymbolic-functions -Wl,-z,relro\"",
    ],
    "//conditions:default": [],
}) + select({
    "@kong//:wasmx_flag": [
        "--add-module=$$EXT_BUILD_ROOT$$/external/ngx_wasm_module",
    ],
    "//conditions:default": [],
}) + select({
    "@kong//:wasmx_v8": [
        "--with-cc-opt=\"-I$$EXT_BUILD_ROOT$$/external/v8/include\"",
        "--with-ld-opt=\"-L$$EXT_BUILD_ROOT$$/external/v8/lib\"",
        "--with-ld-opt=\"-Wl,-rpath,$$EXT_BUILD_ROOT$$/external/v8/lib\"",
    ],
    "//conditions:default": [],
}) + select({
    "@kong//:wasmx_wasmer": [
        "--with-cc-opt=\"-I$$EXT_BUILD_ROOT$$/external/wasmer/include\"",
        "--with-ld-opt=\"-L$$EXT_BUILD_ROOT$$/external/wasmer/lib\"",
        "--with-ld-opt=\"-Wl,-rpath,$$EXT_BUILD_ROOT$$/external/wasmer/lib\"",
    ],
    "//conditions:default": [],
}) + select({
    "@kong//:wasmx_wasmtime": [
        "--with-cc-opt=\"-I$$EXT_BUILD_ROOT$$/external/wasmtime/include\"",
        "--with-ld-opt=\"-L$$EXT_BUILD_ROOT$$/external/wasmtime/lib\"",
        "--with-ld-opt=\"-Wl,-rpath,$$EXT_BUILD_ROOT$$/external/wasmtime/lib\"",
    ],
    "//conditions:default": [],
})

# TODO: set prefix to populate pid_path, conf_path, log_path etc

filegroup(
    name = "all_srcs",
    srcs = glob(
        include = ["**"],
        exclude = ["*.bazel"],
    ),
)

configure_make(
    name = "openresty",
    build_data = [
        "@lua-kong-nginx-module//:all_srcs",
        "@lua-resty-lmdb//:all_srcs",
        "@lua-resty-events//:all_srcs",
        "@openresty_binding//:all_srcs",
        "@ngx_wasm_module//:all_srcs",
        "@v8//:all_srcs",
        "@wasmer//:all_srcs",
        "@wasmtime//:all_srcs",
    ],
    env = select({
        "@kong//:wasmx_v8": {
            "NGX_WASM_RUNTIME": "v8",
        },
        "@kong//:wasmx_wasmer": {
            "NGX_WASM_RUNTIME": "wasmer",
        },
        "@kong//:wasmx_wasmtime": {
            "NGX_WASM_RUNTIME": "wasmtime",
        },
        "//conditions:default": {},
    }),
    configure_command = "configure",
    configure_in_place = True,
    configure_options = CONFIGURE_OPTIONS,
    lib_source = ":all_srcs",
    out_bin_dir = "",
    out_binaries = [
        "nginx/sbin/nginx",
    ],
    targets = [
        "-j" + KONG_VAR["NPROC"],
        "install -j" + KONG_VAR["NPROC"],
    ],
    visibility = ["//visibility:public"],
    deps = [
        "@pcre",
        "@openresty//:luajit",
        "@openssl//:openssl",
    ] + select({
        "@kong//:any-cross": [
            "@cross_deps_zlib//:zlib",
        ],
        "//conditions:default": [],
    }) + select({
        # any cross build that migrated to use libxcrypt needs those flags
        # alpine uses different libc so doesn't need it
        "@kong//:arm64-linux-gnu-cross": [
            "@cross_deps_libxcrypt//:libxcrypt",
        ],
        "//conditions:default": [],
    }),
)
