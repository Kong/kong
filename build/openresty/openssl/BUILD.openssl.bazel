"""An openssl build file based on a snippet found in the github issue:
https://github.com/bazelbuild/rules_foreign_cc/issues/337

Note that the $(PERL) "make variable" (https://docs.bazel.build/versions/main/be/make-variables.html)
is populated by the perl toolchain provided by rules_perl.
"""

load("@rules_foreign_cc//foreign_cc:defs.bzl", "configure_make")
load("@kong_bindings//:variables.bzl", "KONG_VAR")

# Read https://wiki.openssl.org/index.php/Compilation_and_Installation

filegroup(
    name = "all_srcs",
    srcs = glob(
        include = ["**"],
        exclude = ["*.bazel"],
    ),
)

CONFIGURE_OPTIONS = [
    "-g",
    "shared",
    "-DPURIFY",
    "no-threads",
    "no-unit-test",
    "--openssldir=%s/kong" % KONG_VAR["INSTALL_DESTDIR"],
    "-Wl,-rpath,%s/kong/lib" % KONG_VAR["INSTALL_DESTDIR"],
] + select({
    "@kong//:debug_flag": ["-d"],
    "//conditions:default": [],
})

configure_make(
    name = "openssl",
    configure_command = "config",
    configure_in_place = True,
    configure_options = CONFIGURE_OPTIONS,
    env = select({
        "@platforms//os:macos": {
            "AR": "",
        },
        "@kong//:arm64-linux-gnu-cross": {
            "MACHINE": "aarch64",
            "SYSTEM": "linux2",
        },
        # no extra args needed for "@kong//:x86_64-linux-musl-cross"
        "//conditions:default": {},
    }),
    lib_source = ":all_srcs",
    out_binaries = ["openssl"],
    # Note that for Linux builds, libssl must come before libcrypto on the linker command-line.
    # As such, libssl must be listed before libcrypto
    out_shared_libs = select({
        "@platforms//os:macos": [
            "libssl.1.1.dylib",
            "libcrypto.1.1.dylib",
        ],
        "//conditions:default": [
            "libssl.so.1.1",
            "libcrypto.so.1.1",
        ],
    }),
    targets = [
        "-j" + KONG_VAR["NPROC"],
        "install_sw",
    ],
    # TODO: uncomment this to allow bazel build a perl if not installed on system
    # toolchains = ["@rules_perl//:current_toolchain"],
    visibility = ["//visibility:public"],
)

filegroup(
    name = "gen_dir",
    srcs = [":openssl"],
    output_group = "gen_dir",
    visibility = ["//visibility:public"],
)
